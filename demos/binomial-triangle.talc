#!/usr/bin/talc

# Prints Pascal's triangle.
# http://en.wikipedia.org/wiki/Pascal%27s_triangle

size := 13;

# FIXME: list should have something like n_copies(n: int, item: T).
function list<int> n_zeros(n: int) {
    result: list<int> = [];
    for (i := 0; i < n; ++i) {
        result.push_back(0);
    }
    return result;
}

# FIXME: list (and any other mutable containers) should have something like copy().
function list<int> copy(l: list<int>) {
    result: list<int> = [];
    for (i := 0; i< l.size(); ++i) {
        result.push_back(l[i]);
    }
    return result;
}

# FIXME: string should have something like n_copies(n: int, s: string).
function string n_spaces(n: int) {
    result := '';
    for (i := 0; i < n; ++i) {
        result += ' ';
    }
    return result;
}

prev: list<int> = n_zeros(size);
next: list<int> = n_zeros(size);
next[0] = 1;
for (i := 0; i < size-1; ++i) {
    print(n_spaces((size*3)-3*i), next[0]);
    if (i > 0) {
        for (j := 1; j <= i; ++j) {
            next[j] = prev[j] + prev[j-1];
            # FIXME: we could do with some kind of "format using" here.
            s := next[j].to_s();
            s = n_spaces(6 - s.size()) + s;
            print(s);
        }
    }
    puts();
    prev = copy(next);
}
exit(0);
